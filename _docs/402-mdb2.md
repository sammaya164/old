---
title: "Access形式データベースへの接続2"
permalink: /mdb2/
last_modified_at: 2022-11-13T11:00:00+09:00
toc: true
---

## レコードの追加、削除、参照

以下いずれのスクリプトファイルも、前回作成したtest.mdbと同じフォルダに保存し、前回同様32bit版WSHから実行します。


## レコードを追加する

```vb
Dim query
Dim activeConnection
Dim cursorType
Dim lockType

query = "data" 'テーブル名
activeConnection = "Provider=Microsoft.JET.OLEDB.4.0;Data Source=test.mdb;" '接続文字列
cursorType = 1 'adOpenKeyset
lockType   = 3 'adLockOptimistic


Dim rs
Set rs  = CreateObject("ADODB.Recordset")

'レコードセットを取得
rs.Open query, activeConnection, cursorType, lockType


'レコードを追加
rs.AddNew()
rs.Fields("整数") = 2
rs.Fields("テキスト") = "two"

rs.AddNew()
rs.Fields("整数") = 3
rs.Fields("テキスト") = "three"

rs.AddNew()
rs.Fields("整数") = 10
rs.Fields("テキスト") = "ten"

rs.Update 'データを更新

'レコードセットを閉じる
rs.Close 

Msgbox "完了しました"

```

## 条件に合致するレコードを1件削除する

```vb
Dim query
Dim activeConnection
Dim cursorType
Dim lockType

query = "data" 'テーブル名
activeConnection = "Provider=Microsoft.JET.OLEDB.4.0;Data Source=test.mdb;" '接続文字列
cursorType = 1 'adOpenKeyset
lockType   = 3 'adLockOptimistic


Dim rs
Set rs  = CreateObject("ADODB.Recordset")

'レコードセットを取得
rs.Open query, activeConnection, cursorType, lockType


'先頭レコードが条件に合致する場合は削除
If rs.Fields("整数")=2 Then
    rs.Delete

'そうでなければ条件に合致するレコードを探して削除する
Else
    rs.Find "整数 = 2", 1 '第2引数はステップ数、省略するとと無限ループになる
    If Not rs.EOF Then rs.Delete 'レコードが見つかった場合は削除

End If


'レコードセットを閉じる
rs.Close 

Msgbox "完了しました"

```

## 条件に合致するレコードを全件削除する

```vb
Dim query
Dim activeConnection
Dim cursorType
Dim lockType

query = "SELECT * FROM data WHERE 整数=10" 'クエリー
activeConnection = "Provider=Microsoft.JET.OLEDB.4.0;Data Source=test.mdb;" '接続文字列
cursorType = 1 'adOpenKeyset
lockType   = 3 'adLockOptimistic


Dim rs
Set rs  = CreateObject("ADODB.Recordset")

'レコードセットを取得
rs.Open query, activeConnection, cursorType, lockType


'レコードをすべて削除
Do Until rs.EOF
    rs.DELETE
    rs.MoveNext
Loop


'レコードセットを閉じる
rs.Close

Msgbox "完了しました"

```

## レコードを参照する

```vb
Dim query
Dim activeConnection
Dim cursorType
Dim lockType

query = "data" 'テーブル名
activeConnection = "Provider=Microsoft.JET.OLEDB.4.0;Data Source=test.mdb;" '接続文字列
cursorType = 0 'adOpenForwardOnly
lockType   = 1 'adLockReadOnly


Dim rs
Set rs  = CreateObject("ADODB.Recordset")

'レコードセットを取得
rs.Open query, activeConnection, cursorType, lockType


Dim buf
Dim fld

'フィールド名を取得
For Each fld In rs.Fields
    buf = buf & fld.Name & " "
Next

buf = buf & vbCr

'値を取得
Do Until rs.EOF
    For Each fld In rs.Fields
        buf = buf & fld.Value & " "
    Next

    buf = buf & vbCr
    rs.MoveNext
Loop


'レコードセットを閉じる
rs.Close

Msgbox buf '結果を表示する

```
